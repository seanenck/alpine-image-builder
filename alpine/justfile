tag     := "3.20"
version := tag + ".3"
arch    := "aarch64"
target  := `pwd` / "build/"
binary  := `pwd` / ".." / "target" / "alpine-image-builder"
dltargz := target / "files.tar.gz"
scripts := target / "scripts/"
url     := "https://gitlab.alpinelinux.org/alpine/aports/-/archive/" + tag + "-stable/aports-" + tag + "-stable.tar.gz"

default: build

[no-cd]
build:
    @just scripts
    {{binary}} --config builder.toml --arch {{arch}} --version {{version}} --output {{target}} --aports "{{scripts}}"

scripts:
    @rm -rf {{scripts}}
    @mkdir -p "{{scripts}}"
    @test -e "{{dltargz}}" || wget -O "{{dltargz}}" "{{url}}"
    @tar xf "{{dltargz}}" --strip-components=1 -C "{{scripts}}"

clean:
    rm -rf "{{target}}"
